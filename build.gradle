plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
}

repositories {
    mavenLocal()
    jcenter()
}

group 'net.christophermerrill'
version = '1.0'

dependencies {
    implementation 'org.update4j:update4j:1.4.5'
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
    }

javadoc {
    options.addBooleanOption('html5', true)
    options.addStringOption('Xdoclint:none', '-quiet')
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
    }

artifacts
    {
    archives sourcesJar
    archives javadocJar
    }

wrapper {
    gradleVersion = '6.3'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'update4j-multichannel' // pom.project.name must be same as bintray.pkg.name
                description = 'A wrapper around update4j to provide multiple release channels.'
                url = 'https://github.com/ChrisLMerrill/update4j-multichannel'
                packaging = 'jar'
                groupId = project.group
                artifactId = 'update4j-multichannel'
                version = project.version
                inceptionYear = '2020' // HARDCODED
                licenses {
                    license { // HARDCODED
                        name = 'Apache-2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = "ChrisLMerrill"
                        name = "Chris Merrill"
                        email = "osdev@christophermerrill.net"
                    }
                }
                scm {
                    connection = 'https://github.com/ChrisLMerrill/update4j-multichannel.git'
                    developerConnection = 'https://github.com/ChrisLMerrill'
                    url = 'https://github.com/ChrisLMerrill/update4j-multichannel.git'
                }
            }
        }
    }
}

bintray {
    user = BINTRAY_UPLOAD_USERNAME
    key = BINTRAY_UPLOAD_APIKEY
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = 'update4j-multichannel'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/ChrisLMerrill/update4j-multichannel.git'
        version {
            name = project.version
            released  = new Date()
            gpg {
                sign = true
                passphrase = GPG_JARSIGN_PASSPHRASE
            }
            mavenCentralSync {
                sync = true //[Default: true] Determines whether to sync the version to Maven Central.
                user = MAVENCENTRAL_USERNAME
                password = MAVENCENTRAL_PASSWORD
                close = '1' //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
            }
        }
    }
}